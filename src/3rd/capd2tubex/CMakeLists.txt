# ==================================================================
#  tubex-3rd-capd - cmake configuration file
# ==================================================================

list(APPEND SRC ${CMAKE_CURRENT_SOURCE_DIR}/tubex_capd2tubex.cpp
                ${CMAKE_CURRENT_SOURCE_DIR}/tubex_capd2tubex.h
                )


################################################################################
# Create the target for libtubex-3rd-capd
################################################################################

add_library(tubex-3rd-capd ${SRC})
target_compile_options(tubex-3rd-capd PUBLIC ${TUBEX_CFLAGS})

# todo: find a clean way to access tubex header files?
set(TUBEX_HEADERS_DIR ${CMAKE_CURRENT_BINARY_DIR}/../../../include)
file(MAKE_DIRECTORY ${TUBEX_HEADERS_DIR})

target_include_directories(tubex-3rd-capd SYSTEM PUBLIC ${IBEX_INCLUDE_DIRS})
target_include_directories(tubex-3rd-capd PUBLIC ${TUBEX_HEADERS_DIR}
                                                 ${CMAKE_CURRENT_SOURCE_DIR})
target_link_libraries(tubex-3rd-capd PUBLIC ${IBEX_LDFLAGS} ${TUBEX_LDFLAGS})


################################################################################
# Installation of libtubex-3rd-capd files
################################################################################

foreach(srcfile ${SRC})
  if(srcfile MATCHES "\\.h$" OR srcfile MATCHES "\\.hpp$")
    list(APPEND HDR ${srcfile})
    file(COPY ${srcfile} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../include)
  endif()
endforeach()

# Generates a tubex-3rd-capd.h file
set(TUBEX_3RD_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/tubex-3rd-capd.h)
file(WRITE ${TUBEX_3RD_MAIN_HEADER} "/* This file is generated by CMake */\n\n")
file(APPEND ${TUBEX_3RD_MAIN_HEADER} "#ifndef __TUBEX_3RD_H__\n#define __TUBEX_3RD_H__\n\n")
foreach(header_path ${HDR})
  get_filename_component(header_name ${header_path} NAME)
  file(APPEND ${TUBEX_3RD_MAIN_HEADER} "#include <${header_name}>\n")
endforeach()
file(APPEND ${TUBEX_3RD_MAIN_HEADER} "\n#endif /* __TUBEX_3RD_H__ */\n")
file(COPY ${TUBEX_3RD_MAIN_HEADER} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../include)

# Install files in system directories
install(TARGETS tubex-3rd-capd DESTINATION ${CMAKE_INSTALL_LIBDIR})
install(FILES ${HDR} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/tubex-3rd-capd)
install(FILES ${TUBEX_3RD_MAIN_HEADER} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/tubex-3rd-capd)